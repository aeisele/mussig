plugins {
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
    id 'java'
    id 'org.liquibase.gradle' version "$liquibaseGradleVersion"
    id "io.freefair.lombok" version "$lombokGradleVersion"
    id 'nu.studer.jooq' version "$jooqGradleVersion"
}

group = 'com.andreaseisele.mussig'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '14'

ext {
    jdbcDriver = 'org.h2.Driver'
    jdbcUrl = 'jdbc:h2:file:./mussigdb'
    jdbcUser = 'sa'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'org.slf4j:jul-to-slf4j'

    // update to proper 2.2.6 release once available
    implementation 'org.bitbucket.ijabz:jaudiotagger:master-SNAPSHOT'
    implementation "com.google.jimfs:jimfs:$jimFsVersion"
    implementation "org.modelmapper:modelmapper:$modelMapperVersion"

    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'com.h2database:h2'
    liquibaseRuntime 'org.liquibase:liquibase-core'
    liquibaseRuntime "org.liquibase:liquibase-groovy-dsl:$liquibaseGroovyDslVersion"
    liquibaseRuntime 'com.h2database:h2'
    liquibaseRuntime 'ch.qos.logback:logback-classic'

    jooqRuntime 'com.h2database:h2'
    jooqRuntime "com.github.aeisele:jooqdepluralizestrategy:$jooqDepluralizeVersion"

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation("com.google.truth:truth:$googleTruthVersion")
    testImplementation("com.google.truth.extensions:truth-java8-extension:$googleTruthVersion")

}

test {
    useJUnitPlatform()
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
            url project.ext.jdbcUrl
            username project.ext.jdbcUser
        }
    }
}

jooq {
    version = dependencyManagement.importedProperties['jooq.version']
    domain(sourceSets.main) {
        jdbc {
            driver = project.ext.jdbcDriver
            url = project.ext.jdbcUrl
            user = project.ext.jdbcUser
        }
        generator {
            strategy {
                name = 'com.andreaseisele.jooq.strategy.DepluralizeNamingStrategy'
            }
            database {
                name = 'org.jooq.meta.h2.H2Database'
                inputSchema = 'PUBLIC'
                includes = '.*'
                excludes = 'DATABASECHANGELOG | DATABASECHANGELOGLOCK'
            }
            generate {
                daos = true
                springAnnotations = true
            }
            target {
                packageName = 'com.andreaseisele.mussig.persistence.model'
            }
        }
    }
}

generateDomainJooqSchemaSource.dependsOn update
plugins {
    id 'org.springframework.boot' version '2.2.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'org.liquibase.gradle' version '2.0.1'
    id "io.freefair.lombok" version "4.1.2"
    id 'nu.studer.jooq' version '3.0.3'
}

group = 'com.andreaseisele.mussig'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

ext {
    jdbcDriver = 'org.h2.Driver'
    jdbcUrl = 'jdbc:h2:file:./mussigdb'
    jdbcUser = 'sa'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'org.slf4j:jul-to-slf4j'

    // update to proper 2.2.6 release once available
    implementation 'org.bitbucket.ijabz:jaudiotagger:master-SNAPSHOT'

    implementation 'com.google.jimfs:jimfs:1.1'

    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'com.h2database:h2'
    liquibaseRuntime 'org.liquibase:liquibase-core'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.0.3'
    liquibaseRuntime 'com.h2database:h2'
    liquibaseRuntime 'ch.qos.logback:logback-classic'
    jooqRuntime 'com.h2database:h2'
    jooqRuntime 'com.github.aeisele:jooqdepluralizestrategy:1.0'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation('com.google.truth:truth:1.0')
    testImplementation('com.google.truth.extensions:truth-java8-extension:1.0')

}

test {
    useJUnitPlatform()
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db/changelog/changelog-master.xml'
            url project.ext.jdbcUrl
            username project.ext.jdbcUser
        }
    }
}

jooq {
    domain(sourceSets.main) {
        jdbc {
            driver = project.ext.jdbcDriver
            url = project.ext.jdbcUrl
            user = project.ext.jdbcUser
        }
        generator {
            strategy {
                name = 'com.andreaseisele.jooq.strategy.DepluralizeNamingStrategy'
            }
            database {
                name = 'org.jooq.meta.h2.H2Database'
                inputSchema = 'PUBLIC'
                includes = '.*'
                excludes = 'DATABASECHANGELOG | DATABASECHANGELOGLOCK'
            }
            generate {
                daos = true
                springAnnotations = true
            }
            target {
                packageName = 'com.andreaseisele.mussig.persistence.model'
            }
        }
    }
}